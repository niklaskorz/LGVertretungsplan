apply plugin: 'com.android.application'

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    defaultConfig {
        applicationId "de.niklaskorz.lgvertretungsplan"
        minSdkVersion 16
        targetSdkVersion 21

        def longVersionName = "git -C ${rootDir} describe --tags --long".execute().text.trim()
        def (fullVersionTag, versionBuild, gitSha) = longVersionName.tokenize('-')
        def (versionMajor, versionMinor, versionPatch) = fullVersionTag.tokenize('.')

        versionName "$versionMajor.$versionMinor.$versionPatch.$versionBuild:$gitSha"
        versionCode versionMajor.toInteger() * 100000 +
                versionMinor.toInteger() * 10000 +
                versionPatch.toInteger() * 1000 +
                versionBuild.toInteger()
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

repositories {
    maven {
        url 'https://dl.bintray.com/drummer-aidan/maven'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile('com.mikepenz.materialdrawer:library:2.0.5@aar') {
        transitive = true
    }
    compile 'com.android.support:appcompat-v7:22.0.0'
    compile 'com.nispok:snackbar:2.10.6'
    compile 'org.jsoup:jsoup:1.8.2'
    compile 'com.loopj.android:android-async-http:1.4.5'
    compile 'com.android.support:recyclerview-v7:22.0.0'
    compile 'com.jakewharton:butterknife:6.1.0'
    compile 'com.google.guava:guava:18.0'
    compile 'com.google.android.gms:play-services:7.0.0'
    compile 'com.afollestad:material-dialogs:0.7.2.4'
}
