import groovy.json.JsonBuilder

apply plugin: 'com.android.application'

class Version {
    String longVersionName, fullVersionTag
    String major, minor, patch, buildNumber, commit
    String changes

    Version(File dir) {
        longVersionName = "git -C $dir describe --tags --long".execute().text.trim()
        (fullVersionTag, buildNumber, commit) = longVersionName.tokenize('-')
        (major, minor, patch) = fullVersionTag.tokenize('.')

        changes = ["git", "-C", dir, "log", "$fullVersionTag..HEAD", "--pretty=format:- %s"].execute().text.trim()
    }

    String toString() {
        "$major.$minor.$patch ($buildNumber:$commit)"
    }

    int toInteger() {
        major.toInteger() * 100000 + minor.toInteger() * 10000 + patch.toInteger() * 1000 + buildNumber.toInteger()
    }

    void writeVersion(File file) {
        file.text = this.toInteger()
    }

    void writeInfo(File file) {
        def json = new JsonBuilder()
        json {
            version this.toString()
            description changes
        }

        file.text = json.toString()
    }
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.1"
    useLibrary 'org.apache.http.legacy'

    defaultConfig {
        applicationId "de.niklaskorz.lgvertretungsplan"
        minSdkVersion 16
        targetSdkVersion 23

        def version = new Version(rootDir)
        versionName version.toString()
        versionCode version.toInteger()

        printf("\n------- VERSION DATA -------\n- CODE: " + versionCode + "\n" +
                "- NAME: " + versionName + "\n----------------------------\n")

        mkdir "$rootDir/versions"
        version.writeVersion file("$rootDir/versions/version.txt")
        version.writeInfo file("$rootDir/versions/${version.toInteger()}.json")
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix " DEBUG"
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

repositories {
    jcenter()
    maven {
        url 'https://jitpack.io'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile('com.afollestad.material-dialogs:core:0.8.3.0@aar') {
        transitive = true
    }
    compile('com.afollestad.material-dialogs:commons:0.8.3.0@aar') {
        transitive = true
    }
    compile 'com.android.support:appcompat-v7:23.0.1'
    compile 'com.nispok:snackbar:2.10.6'
    compile 'org.jsoup:jsoup:1.8.2'
    compile 'com.loopj.android:android-async-http:1.4.5'
    compile 'com.android.support:recyclerview-v7:23.0.1'
    compile 'com.jakewharton:butterknife:6.1.0'
    compile 'com.google.guava:guava:18.0'
    compile 'com.android.support:support-v4:23.0.1'
    compile 'com.yqritc:recyclerview-flexibledivider:1.2.4'
}










