import groovy.json.JsonBuilder

apply plugin: 'com.android.application'

class Version {
    String longVersionName, fullVersionTag
    String major, minor, patch, buildNumber, commit
    String changes

    Version(File dir) {
        longVersionName = "git -C $dir describe --tags --long".execute().text.trim()
        (fullVersionTag, buildNumber, commit) = longVersionName.tokenize('-')
        (major, minor, patch) = fullVersionTag.tokenize('.')

        changes = ["git", "-C", dir, "log", "$fullVersionTag..HEAD", "--pretty=format:- %s"].execute().text.trim()
    }

    String toString() {
        "$major.$minor.$patch ($buildNumber:$commit)"
    }

    int toInteger() {
        major.toInteger() * 100000 + minor.toInteger() * 10000 +  patch.toInteger() * 1000 +  buildNumber.toInteger()
    }

    void writeVersion(File file) {
        file.text = this.toInteger()
    }

    void writeInfo(File file) {
        def json = new JsonBuilder()
        json {
            version this.toString()
            description changes
        }

        file.text = json.toString()
    }
}

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    defaultConfig {
        applicationId "de.niklaskorz.lgvertretungsplan"
        minSdkVersion 16
        targetSdkVersion 21

        def version = new Version(rootDir)
        versionName version.toString()
        versionCode version.toInteger()

        printf("\n------- VERSION DATA -------\n- CODE: " + versionCode + "\n" +
                "- NAME: " + versionName + "\n----------------------------\n")

        version.writeVersion file('version.txt')
        version.writeInfo file(version.toInteger() + '.json')
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix " DEBUG"
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

repositories {
    maven {
        url 'https://dl.bintray.com/drummer-aidan/maven'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:22.0.0'
    compile 'com.nispok:snackbar:2.10.6'
    compile 'org.jsoup:jsoup:1.8.2'
    compile 'com.loopj.android:android-async-http:1.4.5'
    compile 'com.android.support:recyclerview-v7:22.0.0'
    compile 'com.jakewharton:butterknife:6.1.0'
    compile 'com.google.guava:guava:18.0'
    compile 'com.google.android.gms:play-services:7.0.0'
    compile 'com.afollestad:material-dialogs:0.7.2.4'
    compile 'com.android.support:support-v4:22.0.0'
}
